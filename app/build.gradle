apply plugin: 'com.android.application'
apply plugin: 'jp.leafytree.android-scala'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "jp.leafytree.gradle:gradle-android-scala-plugin:1.4"
    }
}

dependencies {
    // Downgraded to work around compiler error
    // https://github.com/saturday06/gradle-android-scala-plugin/issues/64
    compile "com.android.support:appcompat-v7:22.1.0"
    compile 'com.android.support:multidex:1.0.1'
    androidTestCompile "com.android.support:multidex-instrumentation:1.0.1", { exclude module: "multidex" }
    compile "org.scala-lang:scala-library:2.11.6"
    compile 'com.google.guava:guava:18.0'
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.nutomic.ensichat"
        targetSdkVersion 22
        versionCode 3
        versionName "0.1.2"
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFile file("proguard-rules.pro")
        }
    }

    // Increasing minSdkVersion reduces compilation time for MultiDex.
    productFlavors {
        dev.minSdkVersion 21
        rel.minSdkVersion 15
    }

    // Avoid duplicate file errors during packaging.
    packagingOptions {
        exclude 'decoder.properties'
        exclude 'rootdoc.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useCompileDaemon = true
}
