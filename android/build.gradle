apply plugin: 'com.android.application'
apply plugin: 'jp.leafytree.android-scala'

dependencies {
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'org.scala-lang:scala-library:2.11.7'
    compile 'com.mobsandgeeks:adapter-kit:0.5.3'
    compile 'com.google.zxing:android-integration:3.3.0'
    compile 'com.google.zxing:core:3.3.0'
    compile 'org.slf4j:slf4j-android:1.7.21'
    compile project(path: ':core')
    androidTestCompile 'com.android.support:multidex-instrumentation:1.0.1',
            { exclude module: 'multidex' }
}

// RtlHardcoded behaviour differs between target API versions. We only care about API 15.
preBuild.doFirst {
    android.applicationVariants.each { variant ->
        if (variant.name == 'devDebug' || variant.name == 'devRelease') {
            println variant.name
            android.lintOptions.disable 'RtlHardcoded'
        }
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.nutomic.ensichat"
        targetSdkVersion 24
        versionCode 17
        versionName "0.5.2"
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }
    }

    // Increasing minSdkVersion reduces compilation time for MultiDex.
    productFlavors {
        dev.minSdkVersion 21
        rel.minSdkVersion 15
    }
}
